package com.athena.mis.accounting.actions.accCancelledVoucher

import com.athena.mis.ActionIntf
import com.athena.mis.BaseService
import com.athena.mis.GridEntity
import com.athena.mis.accounting.entity.AccCancelledVoucher
import com.athena.mis.accounting.utility.AccSessionUtil
import com.athena.mis.accounting.utility.AccVoucherTypeCacheUtility
import com.athena.mis.application.entity.SystemEntity
import com.athena.mis.application.service.SystemEntityService
import com.athena.mis.utility.DateUtility
import com.athena.mis.utility.Tools
import org.apache.log4j.Logger
import org.codehaus.groovy.grails.web.servlet.mvc.GrailsParameterMap
import org.springframework.beans.factory.annotation.Autowired
import org.springframework.transaction.annotation.Transactional

class ListAccCancelledVoucherActionService extends BaseService implements ActionIntf {

    SystemEntityService systemEntityService
    @Autowired
    AccVoucherTypeCacheUtility accVoucherTypeCacheUtility
    @Autowired
    AccSessionUtil accSessionUtil

    private static final String SERVER_ERROR_MESSAGE = "Fail to get cancelled voucher list"
    private static final String CANCELLED_VOUCHER_LIST = "voucherList"
    private static final String COUNT = "count"
    private static final String GRID_OBJ = "gridObj"

    private Logger log = Logger.getLogger(getClass())

    /**
     * do nothing for pre operation
     */
    public Object executePreCondition(Object parameters, Object obj) {
        return null
    }

    /**
     * Get account cancelled voucher (accCancelledVoucher) list for grid
     * @param params -serialized parameters from UI
     * @param obj -N/A
     * @return -a map containing all objects necessary for buildSuccessResultForUI
     * map contains isError(true/false) depending on method success
     */
    @Transactional(readOnly = true)
    public Object execute(Object params, Object obj) {
        LinkedHashMap result = new LinkedHashMap()
        try {
            GrailsParameterMap parameterMap = (GrailsParameterMap) params
            initPager(parameterMap)  // initialize parameters for flexGrid
            long companyId = accSessionUtil.appSessionUtil.getCompanyId()

            // Get cancelled voucher list by using dynamic finder
            List<AccCancelledVoucher> cancelledVoucherList = AccCancelledVoucher.findAllByCompanyId(companyId, [offset: start, max: resultPerPage, sort: sortColumn, order: sortOrder, readOnly: true])
            int total = (int) AccCancelledVoucher.countByCompanyId(companyId)

            result.put(CANCELLED_VOUCHER_LIST, cancelledVoucherList)
            result.put(COUNT, total)
            result.put(Tools.IS_ERROR, Boolean.FALSE)
            return result
        } catch (Exception ex) {
            log.error(ex.getMessage())
            result.put(Tools.IS_ERROR, Boolean.TRUE)
            result.put(Tools.MESSAGE, SERVER_ERROR_MESSAGE)
            return result
        }
    }

    /**
     * do nothing for post operation
     */
    public Object executePostCondition(Object parameters, Object obj) {
        return null
    }

    /**
     * Wrap account cancelled voucher (accCancelledVoucher) list for grid
     * @param obj -map returned from execute method
     * @return -a map containing all objects necessary for grid view
     */
    public Object buildSuccessResultForUI(Object obj) {
        LinkedHashMap result = new LinkedHashMap()
        try {
            LinkedHashMap receiveResult = (LinkedHashMap) obj   // cast map returned from execute method
            int count = (int) receiveResult.get(COUNT)
            List<AccCancelledVoucher> accCancelledVouchers = (List<AccCancelledVoucher>) receiveResult.get(CANCELLED_VOUCHER_LIST)
            List voucherListWrap = wrapVoucherInGridEntityList(accCancelledVouchers, start)
            Map gridObj  = [page: pageNumber, total: count, rows: voucherListWrap]
            result.put(Tools.IS_ERROR, Boolean.FALSE)
            result.put(GRID_OBJ, gridObj)
            return result
        } catch (Exception ex) {
            log.error(ex.getMessage())
            result.put(Tools.IS_ERROR, Boolean.TRUE)
            result.put(Tools.MESSAGE, SERVER_ERROR_MESSAGE)
            return result
        }
    }

    /**
     * Build failure result in case of any error
     * @param obj -map returned from previous methods, can be null
     * @return -a map containing isError = true & relevant error message
     */
    public Object buildFailureResultForUI(Object obj) {
        LinkedHashMap result = new LinkedHashMap()
        try {
            LinkedHashMap receiveResult = (LinkedHashMap) obj // cast map returned from previous method
            result.put(Tools.IS_ERROR, Boolean.TRUE)
            if (receiveResult.get(Tools.MESSAGE)) {
                result.put(Tools.MESSAGE, receiveResult.get(Tools.MESSAGE))
            } else {
                result.put(Tools.MESSAGE, SERVER_ERROR_MESSAGE)
            }
            return result
        } catch (Exception ex) {
            log.error(ex.getMessage())
            result.put(Tools.IS_ERROR, Boolean.TRUE)
            result.put(Tools.MESSAGE, SERVER_ERROR_MESSAGE)
            return result
        }
    }

    /**
     * Wrap list of account cancelled voucher (accCancelledVoucher) in grid entity
     * @param cancelledVoucherList -list of account cancelled voucher (AccCancelledVoucher) object(s)
     * @param start -starting index of the page
     * @return -list of wrapped account cancelled voucher (AccCancelledVoucher)(s)
     */
    private List wrapVoucherInGridEntityList(List<AccCancelledVoucher> cancelledVoucherList, int start) {
        List cancelledVouchers = []
        int counter = start + 1
        AccCancelledVoucher cancelledVoucher
        GridEntity obj
        for (int i = 0; i < cancelledVoucherList.size(); i++) {
            cancelledVoucher = cancelledVoucherList[i]
            obj = new GridEntity()
            obj.id = cancelledVoucher.id
            String instrument = null
            if (cancelledVoucher.instrumentId > 0) {
                SystemEntity instrumentTypeName = systemEntityService.read(cancelledVoucher.instrumentTypeId)
                instrument = instrumentTypeName.key + Tools.COLON + Tools.SINGLE_SPACE + cancelledVoucher.instrumentId
            }
            obj.cell = [
                    counter,
                    cancelledVoucher.id,
                    cancelledVoucher.traceNo,
                    Tools.makeAmountWithThousandSeparator(cancelledVoucher.amount),
                    cancelledVoucher.drCount,
                    cancelledVoucher.crCount,
                    DateUtility.getLongDateForUI(cancelledVoucher.voucherDate),
                    cancelledVoucher.isVoucherPosted ? Tools.YES : Tools.NO,
                    instrument,
                    cancelledVoucher.chequeNo ? cancelledVoucher.chequeNo : Tools.EMPTY_SPACE
            ]
            cancelledVouchers << obj
            counter++
        }
        return cancelledVouchers
    }
}
